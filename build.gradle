plugins {
    id "com.android.library"
    id "kotlin-android"
}

def withPreFabSupport = false

android {
    compileSdkVersion versions.agp.compileSdk
    buildToolsVersion versions.agp.buildTools
    ndkVersion versions.agp.ndk

    defaultConfig {
        minSdkVersion versions.agp.minSdk
        targetSdkVersion versions.agp.targetSdk

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                // Sets a flag to enable format macro constants for the C compiler.
                cFlags "-DHAVE_STDLIB_H -D HAVE_STRING_H -D__STDC_FORMAT_MACROS"

                // Sets optional flags for the C++ compiler.
                cppFlags "-fexceptions", "-frtti", "-std=c++14"

                // Specifies the targets Gradle should build
                targets "agent-ndk" // , "agent-ndk-test"

                // Passes optional arguments to CMake.
                arguments "-DAGENT_VERSION=${project.version}"
            }
        }

        ndk {
            // https://developer.android.com/ndk/guides/abis#sa
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64', 'x86'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version versions.cpp.cmake
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    /** AGP 4.1:
    // Enable generation of Prefab packages and include them in the library's AAR.
    buildFeatures {
        prefabPublishing true
    }

    /*
    // Include the "agent-ndk" module from the native build system in the AAR,
    // and export the headers in src/main/cpp/include to its consumers
    prefab {
        agent-ndk {
            headers "src/main/cpp/include"
        }
    }
    /**/

    // Avoid packing the unnecessary libraries into final AAR. For details
    // refer to https://issuetracker.google.com/issues/168777344#comment5
    // Note that if your AAR also contains Java/Kotlin APIs, you should not
    // exclude libraries that are used by those APIs.

    packagingOptions {
       exclude("**/libc++_shared.so")
    }

    testOptions {
        unitTests {
            all {
                environment 'LD_LIBRARY_PATH', "${buildDir}/intermediates/cmake/debug/obj/x86_64/"
            }
        }
    }
}

dependencies {
    implementation project(path: ':agent-core', configuration: 'uber')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.plugin}"

    // https://android-developers.googleblog.com/2020/02/native-dependencies-in-android-studio-40.html
    if (withPreFabSupport) {
        implementation 'com.android.ndk.thirdparty:jsoncpp:1.8.4-alpha-1'
    }

    testImplementation 'org.robolectric:robolectric:' + project.versions.test.robolectric
    testImplementation 'junit:junit:' + project.versions.test.junit
    testImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    testImplementation 'org.mockito.kotlin:mockito-kotlin:' + project.versions.kotlin.mockito
    testImplementation 'androidx.test:core:' + project.versions.test.androidxCore

}

apply plugin: 'maven'
apply plugin: 'maven-publish'

task install(type: Upload) {
    afterEvaluate() {
        repositories {
            mavenInstaller {
                configuration = configurations.archives
                pom.groupId = 'com.newrelic.agent.android'
                pom.artifactId = 'agent-ndk'
                pom.version = rootProject.version
            }
        }
    }
}


/**
 * kudos: https://sureshjoshi.com/mobile/android-junit-native-libraries/
 */
def osxDir = projectDir.absolutePath + '/.externalNativeBuild/cmake/debug/osx/'

task createBuildDir() {
    def folder = new File(osxDir)
    if (!folder.exists()) {
        folder.mkdirs()
    }
}

task cmake(type: Exec) {
    dependsOn createBuildDir
    workingDir osxDir
    commandLine "/usr/local/bin/cmake"
    args "${projectDir.absolutePath}/src/main/cpp"
}

task cmake_test(type: Exec) {
    dependsOn createBuildDir
    workingDir osxDir
    commandLine "/usr/local/bin/cmake"
    args "${projectDir.absolutePath}/src/test/cpp"
}

task make(type: Exec) {
    dependsOn cmake
    workingDir osxDir
    commandLine "make"
}

project.afterEvaluate {
    // Not sure how much of a hack this is - but it allows CMake/SWIG to run before Android Studio
    // complains about missing generated files
    // TODO: Probably need a release hook too?
    javaPreCompileDebug.dependsOn externalNativeBuildDebug
    // if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    javaPreCompileDebugAndroidTest.dependsOn make
    // }
}