/**
 * Copyright 2021-present New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id "com.android.library"
    id "kotlin-android"
    id 'org.owasp.dependencycheck'
    id 'jacoco'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}

android {
    compileSdkVersion versions.agp.compileSdk
    ndkVersion versions.agp.ndk

    defaultConfig {
        minSdkVersion versions.agp.minSdk
        targetSdkVersion versions.agp.targetSdk

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                // Sets optional flags for the C++ compiler.
                cppFlags "-fexceptions", "-frtti", "-std=c++14", "-funwind-tables"

                // Specifies the targets Gradle should build
                targets "agent-ndk", "agent-ndk-test"

                // Passes optional arguments to CMake.
                arguments "-DCMAKE_BUILD_TYPE=Debug"
            }
        }

        ndk {
            // https://developer.android.com/ndk/guides/abis#sa
            // abiFilters 'x86_64', 'arm64-v8a', 'x86'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
        }

    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version versions.agp.cmake
        }
    }

    packagingOptions {
        doNotStrip '**.so'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                environment 'LD_LIBRARY_PATH', "${buildDir}/intermediates/cmake/debug/obj/x86_64/"
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                    outputs.upToDateWhen { false }
                    showStandardStreams = true
                }
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compileOnly newrelic.deps.agent
    testImplementation newrelic.deps.agent

    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin.plugin}"
    implementation 'com.scottyab:rootbeer-lib:' + project.versions.libraries.rootbeer

    testImplementation 'org.robolectric:robolectric:' + project.versions.test.robolectric
    testImplementation 'junit:junit:' + project.versions.test.junit
    testImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    testImplementation 'org.mockito.kotlin:mockito-kotlin:' + project.versions.kotlin.mockito
    testImplementation 'org.json:json:20190722'

    testImplementation 'androidx.test:core:' + project.versions.test.androidxCore
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    def optionalOf = { key, defaultValue -> rootProject.hasProperty(key) ? rootProject[key] : defaultValue }

    def isSnapshotDeploy = (
            (optionalOf("newrelic.version.build", 'SNAPSHOT') != 'SNAPSHOT') &&
                    (optionalOf("sonatypeUsername", null) != null) &&
                    (optionalOf("sonatypePassword", null) != null))

    publishing {
        publications {
            release(MavenPublication) {
                // The following applies a component to this publication
                // which results in publishing dr app bundle.
                from components.release

                groupId = 'com.newrelic.agent.android'
                artifactId = 'agent-ndk'
                version = project.newrelic.version

                pom {
                    name = 'com.newrelic.agent.android.agent-ndk'
                    description = 'The New Relic Android Native Agent provides crash reporting for Android NDK applications'
                    url = 'https://newrelic.com'
                    packaging = 'aar'

                    licenses {
                        license {
                            url = 'https://github.com/newrelic/android-agent-ndk/blob/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'mobile-agents-team'
                            name = 'Mobile Agents Team'
                            email = 'mobile-agents@newrelic.com'
                        }
                    }

                    scm {
                        url = 'scm:git:https://github.com/newrelic/android-agent-ndk.git'
                        connection = 'scm:git:https://github.com/newrelic/android-agent-ndk.git'
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                project.findProperty("signingKeyId"),
                project.findProperty("signingKey"),
                project.findProperty("signingPassword")
        )
        sign publishing.publications.release
        required {
            gradle.taskGraph.hasTask("publishReleasePublicationToSnapshotsRepository")
        }
    }

    tasks.register("install") {
        dependsOn publishToMavenLocal
        description "Publishes all Maven artifacts produced by this project to the local Maven cache. " +
                "Replacement for the legacy 'maven' plugin task."
        group "publishing"
    }

}
