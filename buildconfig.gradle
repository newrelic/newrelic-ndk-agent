//
// Include this file into the project's root-level build.gradle file.
//
// apply from: "$rootDir/buildconfig.gradle"
//

buildscript {
    ext {
        def hasOptional = { key, defaultValue -> rootProject.hasProperty(key) ? rootProject[key] : defaultValue }

        newrelic = [
                version: hasOptional('newrelic.version', '1.0.1'),
                agent  : [
                        version       : hasOptional('newrelic.agent.version', '6.4.+'),
                        snapshotNumber: hasOptional('newrelic.agent.snapshot', "experimental")
                ]
        ]

        newrelic.agent.snapshotUri = uri("https://oss.sonatype.org/content/repositories/comnewrelic-$newrelic.agent.snapshotNumber")

        newrelic.deps = [
                agent: "com.newrelic.agent.android:android-agent:" + newrelic.agent.version
        ]

        versions = [
                test  : [
                        junit       : '4.12',
                        mockitoCore : '2.28.2',
                        robolectric : '4.6.+',
                        androidxCore: '1.4.+',
                ],
                kotlin: [
                        plugin : "1.4.21",
                        ktx    : "1.3.2",
                        mockito: "3.2.0"
                ],
                agp   : [
                        // https://developer.android.com/studio/releases/platforms
                        plugin    : hasOptional('newrelic.agp.version', '4.1.+'),
                        minSdk    : 21,         // Android 5.0
                        compileSdk: 24,         // Android 7.0
                        targetSdk : 27,         // Android 8.1
                        buildTools: '29.0.2',
                        ndk       : '21.4.7075529',
                        cmake     : '3.18.1'
                ]
        ]
    }
}

allprojects {
    buildscript {
        repositories {
            maven { url newrelic.agent.snapshotUri }
            mavenLocal()
        }
        dependencies {

        }
    }

    repositories {
        google()
        mavenLocal()
    }
}

subprojects {
    ext.newrelic = rootProject.newrelic

    buildscript {
        repositories {
            maven { url newrelic.agent.snapshotUri }
            mavenLocal()
            mavenCentral()
            google()
        }

        dependencies {

        }
    }

    repositories {
        maven { url newrelic.agent.snapshotUri }
        mavenLocal()
        mavenCentral()
        google()
    }

}
