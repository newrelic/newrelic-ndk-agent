/**
 * Copyright 2022-present New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext {
        def hasOptional = { key, defaultValue -> rootProject.hasProperty(key) ? rootProject[key] : defaultValue }

        newrelic = [
                version: hasOptional('newrelic.version', '+'),
                agent: [
                        version       : hasOptional('newrelic.agent.version', '+'),
                        snapshot      : hasOptional('newrelic.agent.snapshot', null),
                ]
        ]

        newrelic.deps = [
                ndk   : "com.newrelic.agent.android:agent-ndk:" + newrelic.version,
                agent : "com.newrelic.agent.android:android-agent:" + newrelic.agent.version,
                plugin: "com.newrelic.agent.android:agent-gradle-plugin:" + newrelic.agent.version
        ]

        versions = [
                test  : [
                        junit       : '4.12',
                        mockitoCore : '2.28.2',
                        robolectric : '4.6.+',
                        androidxCore: '1.4.+',
                        owasp: '8.1.2',
                        jacoco: '0.8.4',
                ],
                kotlin: [
                        plugin : "1.6.0",
                        ktx    : "1.3.2",
                        mockito: "3.2.0"
                ],
                agp   : [
                        // https://developer.android.com/studio/releases/platforms
                        plugin    : hasOptional('newrelic.agp.version', '4.1.+'),
                        minSdk    : 24,         // Android 7.0
                        compileSdk: 28,         // Android 8.1
                        targetSdk : 28,         // Android 8.1
                        buildTools: '29.0.2',
                        ndk       : '21.4.7075529',
                        cmake     : '3.18.1'
                ],
                libraries  : [
                        rootbeer       : '0.1.0',
                ],
                java  : [
                        nexus       : '1.1.0',
                ],
        ]

        newrelic.agent.snapshotUri = uri("https://oss.sonatype.org/content/repositories/comnewrelic-$newrelic.agent.snapshot")
    }

    repositories {
        maven { url newrelic.agent.snapshotUri }
        mavenLocal()
        mavenCentral()
    }
}


subprojects {
    ext.newrelic = rootProject.newrelic

    buildscript {
        repositories {
            maven { url newrelic.agent.snapshotUri }
            mavenLocal()
            mavenCentral()
            google()
        }

        dependencies {
            //noinspection DependencyNotationArgument
            classpath newrelic.deps.plugin
        }
    }

    repositories {
        maven { url newrelic.agent.snapshotUri }
        mavenLocal()
        mavenCentral()
        google()
    }
}
